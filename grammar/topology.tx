TopologyModel:
    defaults=Defaults?
    components=Components?
    events=Events?
;

Defaults:
    'defaults' '{'
        properties*=DefaultProperty
    '}'
;

Components:
    'components' '{'
        components*=Component
    '}'
;

Events:
    'events' '{'
        events*=Event
    '}'
;

Event:
    ElementPropertiesChanged
;

ElementPropertiesChanged:
    "ElementPropertiesChanged" "(" properties*=ElementPropertiesChangedProperty ")"
;

ElementPropertiesChangedProperty:
  name=ElementPropertiesChangedPropertyKeyword value=PropertyValue
;

ElementPropertiesChangedPropertyKeyword:
     EventPropertyKeyword | 'previous' | 'current'
;

Component:
    ComponentMultiline | ComponentInline
;

ComponentMultiline:
   component_type=ComponentType "(" properties*=Property ")"
;

ComponentInline:
   component_type=ComponentType "(" properties*=Property[','] ")"
;

ComponentType:
    /[^\d\W][\w.\s]*\b/
;

Property:
  name=PropertyKeyword value=PropertyValue
;

PropertyKeyword:
     'identifiers' | 'id' | 'name' | 'layer' | 'domain' | 'environment' | 'labels' | 'processor' | 'data' |
     'relations' | 'healthMessage' |'health'
;

DefaultProperty:
  name=DefaultPropertyKeyword value=PropertyValue
;

DefaultPropertyKeyword:
     PropertyKeyword | 'eventProcessor' | 'tags' | 'title' | 'message' | 'links'
;

EventPropertyKeyword:
     'title' | 'message' | 'identifiers' | 'tags' | 'links' | 'processor'
;

PropertyString:
    ID | STRING
;

PropertyValue:
    PropertyString | FLOAT | INT | BOOL | PropertyObject | PropertyList | PropertyCode
;

PropertyList:
    '[' values*=PropertyValue[','] ']'
;

PropertyObject:
    "{" members*=PropertyMember[','] "}"
;

PropertyCode:
   "```" code=/(?ms)(.*?)[`]{3}/
;

PropertyMember:
    key=ID  value=PropertyValue
;

Comment:
  /#.*$/
;